module frech.Eval
        inline (whiteLazyOfficers, blackLazyOfficers, opening, forP) 
    where

import Data.Bits (BitSet, .^. , .&., shiftL, ushiftR)

import frech.Daten

--- Base values for 'Piece's in centipawns 
pieceValue EMPTY  = 0
pieceValue PAWN   = 100
pieceValue KNIGHT = 300
pieceValue BISHOP = 300
pieceValue ROOK   = 500
pieceValue QUEEN  = 800
pieceValue KING   = 1000

--- > p `percent` v
--- computes p percent of v
percent n m  = (n * m) `quot` 100

--- take the value in favor of a certain 'Player'
forP ∷ Player → Int → Int
forP p n = p.factor * n

--- discount n c m
--- discount n% from m if condition holds
discount !n !condition !m = if condition then m - ((m*n) `quot` 100) else m 

--- penalize player condition n
--- If condition holds, count n in favor of players opponent, else 0
penalize ∷ Player → Bool → Int → Int
penalize p c n = if c then forP p.opponent n else 0   

--- replaces 
--- > length (filter (==pos) history)
--- saving thunks 
countEqPos :: Position -> [Position] -> Int
countEqPos !pos !history = go pos history 0
    where
        go ∷ Position → [Position] → Int → Int
        go !p [] !n = n
        go !p (h:hs) !n
            | p == h    = go p hs (n+1)
            | otherwise = go p hs n 

--- replace
--- > pos `elem` positions
--- to save thunks
element ∷ Position → [Position] → Bool
element !p [] = false
element !p (h:hs) = if p == h then true else element p hs   -- enforce tail recursion

--- 'Position' value when 'BLACK' is mate.
!blackIsMate = 0x7fff
--- 'Position' value when 'WHITE' is mate.
!whiteIsMate = -blackIsMate

clearLowestBit f = BitSet (n .^. (n .&. negate n)) where !n = BitSet.set f

{--
    Evaluate a 'Position'

    Positive values indicate a 'Position' favourable for 'WHITE', whereas negative ones indicate 
    'BLACK' is ahead.
    
    Special values:
    
    1. 0 may indicate stalemate, repeated position or draw because of 50 moves rule.
    2. 99999 indicates 'BLACK' is mate.
    3. -99999 indicates 'WHITE' is mate.
-}

--- a bit more expensive, checks special conditions
evalHist :: [Position] → Position → Int
evalHist history pos
    | mate = if pos.turn == BLACK then blackIsMate else whiteIsMate
    | stalemate = 0
    | pos.getPlyCounter >= 100 = 0             -- to be done in higher functions 
    | pos `element` history = 0
    | otherwise = eval pos
    where
        !ms = moves pos
        mate = null ms && check 
        stalemate = null ms && not mate
        check = inCheck pos pos.turn

--- does not detect mate, stalemate or draw by rule
eval ∷ Position  → Int
eval pos = (matDelta * ((max matWhite matBlack * 1000) `quot`  min matWhite matBlack)) `quot` 1000
                    + goHanging  pos pos.figures 0 
                    + forP pos.turn (4 * length mms)
                    + forP pos.turn.opponent (4 * length oms)
                    + forP pos.turn.opponent checkBonus
                    + checkWhiteCastling pos 
                    - checkBlackCastling pos
                    - (whiteBlockedBishopPawns pos * 21)
                    + (blackBlockedBishopPawns pos * 21)
                    - (whiteBlockedBishops pos * 43)
                    + (blackBlockedBishops pos * 43)
                    - (whiteLazyOfficers pos * 15)
                    + (blackLazyOfficers pos * 15)
                    
    where
        -- ms = moves pos
        !oms = if pos.turn == WHITE  then rawMoves pos.{flags = pos.flags `diff`  whiteToMove} 
                                     else rawMoves pos.{flags = pos.flags `union` whiteToMove}
        !mms = rawMoves pos
        check = inCheck pos pos.turn
        checkBonus = if check then 25 else 0
        !matWhite = goMaterial pos pos.whites 0
        !matBlack = goMaterial pos (pos.whites `diff` pos.whites) 0
        matDelta = matWhite - matBlack


--- 'PAWN's that block the 'BISHOP'
!rank2 = BitSet.fromList [B2,D2,E2,G2]
!rank7 = BitSet.fromList [B7,D7,E7,G7]
!whiteOfficers = BitSet.fromList [B1,C1,F1,G1]
!blackOfficers = BitSet.fromList [B8,C8,F8,G8]

checkWhiteCastling, checkBlackCastling, whiteBlockedBishopPawns, blackBlockedBishopPawns,
    whiteBlockedBishops, blackBlockedBishops,
    whiteLazyOfficers, blackLazyOfficers :: Position -> Int

--- Try to enforce castling:
--- Gives a bonus of '50' if WHITE has castled.
--- Otherwise, gives a malus of -50 if 'KING' or both 'ROOK's have moved.
--- Otherwise, gives a malus of -35 if one 'ROOK' has moved, but the 'KING' can still castle with the other
--- Otherwise, gives a malus of -20 if it hasn't castled yet but still  could to both sides.
--- Hence, by castling, one wins 0.7 'PAWN's worth, while by moving the 'KING' prematurely one looses up to 0.5 

checkWhiteCastling pos = castling
    where
        !castling = if castled then 50              -- for having castled 
                    else castlingRights * 15 - 50 
                         --   then negate 50          -- for having moved the king or rooks 
                         --   else negate 20, 35, 50         -- for not having castled yet
        !castled = not (null (pos.flags `inter` Position.whiteHasCastledBits))
        !castlingRights = card (pos.flags `inter` whiteCastlingRights)
        
checkBlackCastling pos = castling
    where
        !castling = if castled then 50              -- for having castled 
                    else castlingRights * 15 - 50
        !castled = not (null (pos.flags `inter` Position.blackHasCastledBits))
        !castlingRights = card (pos.flags `inter` blackCastlingRights)

--- To qualify as opening, a 'Position' must have at least 12 pawns and castling rights for both sides.
--- Put differently, the opening ends 
---     - with the 5th captured pawn, *or*
---     - with castling or loss of castling rights on either side.
--- If the game is in the opening phase, special checks are made.
opening ∷ Position → Bool
opening pos = card pos.pawns >= 12
              &&  not (null (pos.flags `inter` whiteCastlingRights))
              &&  not (null (pos.flags `inter` whiteCastlingRights))

endgame ∷ Position → Bool
endgame pos = card pos.figures < 11 || card pos.pawns < 5

whiteBlockedBishopPawns !pos =
                      (card                                 -- the number of ...
                    . inter pos.figures                     -- the pieces ...
                    . BitSet . (\b → b `shiftL` 8) . _.set)        -- that are directly before ...
            (rank2 `inter` pos.pawns `inter` pos.whites)    -- one of our rank2-pawns 

blackBlockedBishopPawns !pos =
                      (card                                 -- the number of ...
                    . inter pos.figures                     -- the pieces ...
                    . BitSet . (\b -> b `ushiftR` 8) . _.set)       -- that are directly before ...
            ((rank7 `inter` pos.pawns) `diff` pos.whites)   -- one of our rank7-pawns 

whiteBlockedBishops !pos = foldIndexes pos 0 (pos.bishops `inter` pos.whites) -- for all white bishops
    where
        count ∷ Position → Index → Int → Int
        count pos ix n = if (kingTargets ix `inter` bishopTargets ix)   -- the targets around a bishop
                        BitSet.`subset` pos.whites                      -- are they all occupied by whites?  
                        then n+1 else n
        foldFields ∷ (Fields→α→α) → α → Fields → α
        foldFields !f !a !s 
            | null s = a
            | otherwise = foldFields f (f m a) s'
                where
                    !m  = singleton (bitIndex s)
                    s' = s `diff` m
        foldIndexes ∷ Position → Int → Fields → Int
        foldIndexes !pos !a !s 
            | null s = a
            | otherwise = foldIndexes pos (count pos  m a) s'
                where
                    !m  = bitIndex s
                    s' = s `diff` singleton m


blackBlockedBishops !pos = foldIndexes pos 0 (pos.bishops `diff` pos.whites)    -- for all black bishops
    where
        count ∷ Position → Index → Int → Int
        count pos ix n = if (kingTargets ix `inter` bishopTargets ix) -- the targets around a bishop
                        BitSet.`subset` pos.blackFigures              -- are they all occupied by blacks?
                        then n+1 else n
        foldFields ∷ (Fields→α→α) → α → Fields → α
        foldFields !f !a !s 
            | null s = a
            | otherwise = foldFields f (f m a) s'
                where
                    !m  = singleton (bitIndex s)
                    s' = s `diff` m
        foldIndexes ∷ Position → Int → Fields → Int
        foldIndexes !pos !a !s 
            | null s = a
            | otherwise = foldIndexes pos (count pos  m a) s'
                where
                    !m  = bitIndex s
                    s' = s `diff` singleton m

--- count 'BISHOP's and 'KNIGHT's on the home rank in the opening 
whiteLazyOfficers pos 
    | opening pos =     card  (                     -- the number of 
        (pos.bishops `union` pos.knights)           -- officers
        `inter` pos.whites                          -- (white ones)
        `inter` whiteOfficers)                      -- on the home rank
    | otherwise = 0
blackLazyOfficers pos
    | opening pos  =    card  (                     -- the number of 
        ((pos.bishops `union` pos.knights)          -- officers
        `diff` pos.whites)                          -- (black ones)
        `inter` blackOfficers)                      -- on the home rank
    | otherwise = 0


--- compute a value for every 'Piece' and sum it up
goMaterial ∷ Position → Fields → Int → Int
goMaterial pos m n
    | null m = n
    | otherwise = goMaterial pos (m `diff` fx) (n + score pos ix)
    where
        !ix = bitIndex m
        !fx = singleton ix
--- compute the value of a 'Piece'
score ∷ Position → Index → Int
score pos ix = forP fi.player pv
    where
        !pv = pieceValue fi.piece
        !fi = whatIsOn pos ix

--- compute a penalty for insuficiently defended 'Piece's and return the maximum
--- (Remember, if we have two pieces attacked, we still can't sum it up, because one of them will most likely esacpe).
--- However, we slightly increase the maximum to indicate that attacking a ROOK and a KNIGHT is better
--- than just to attack the ROOK.
goHanging ∷ Position → Fields → Int → Int
goHanging pos m n
    | null m = n
    | n != 0    = goHanging pos (m `diff` fx) (110 `percent` (min n (scoreHanging pos ix)))
    | otherwise = goHanging pos (m `diff` fx) (min n (scoreHanging pos ix))
    where
        !ix = bitIndex m
        !fx = singleton ix
--- compute the penalty for  a hanging 'Piece'
scoreHanging ∷ Position → Index → Int
scoreHanging pos ix =  fi.player.factor * discounted 
    where
        !fi = whatIsOn pos ix
        !active = fi.player == pos.turn 
        -- treatment of hanging pieces
        !hanging  = penalty fi.piece  (leastAttacker pos ix fi.player.opponent) 
                                                        (isAttacked pos ix fi.player)
        -- because the active player now has a chance to help that
        !discounted = if active then 25 `percent` hanging else hanging


--- penalty h a d
--- computes the penalty when a 'Piece' h is attacked by a and defended (or not)
penalty hang EMPTY    defended  = 0                     -- not attacked
penalty hang attacker false     = -- e.g. QUEEN can be captured in this move:
                                  -- -(0.7*800) = 560
                                  -- It doesn't help to attack some PAWN as retaliation! 
                                  negate (70 `percent` pieceValue hang)
penalty hang KING     defended         = 0             -- defended piece attacked by KING
penalty hang attacker defended  = if pva >= pvh 
                                    then 0              -- attacker won't capture
                                    else negate (70 `percent` (pvh - pva))
                                    -- e.g. KNIGHT can capture defended ROOK: 
                                    -- -(0.7 * (500 - 300)) = 140
    where
        pva = pieceValue attacker
        pvh = pieceValue hang

